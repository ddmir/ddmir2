(@buy @makedrug )
%100
+0
+42

[@main]
#IF
checkpkpoint 2
#SAY
我不想和你这样的人打交道.. \ \
<退出/@exit>
#ELSEACT
goto @main-1

[@main-1]
你想做什么？\ \
 <炼/@makedrug>药\
 <买/@buy>药\
 <药剂师的信任务/@talkwith>\
 <介绍信任务/@news>(请在15级以前完成)\
 有关毒药<听说明./@LOOKLYSM>\
 <退出/@exit>

[@new3]
#if
#act
take 肉 5
take 鸡肉 5
take 薄荷叶 1
#say
年轻人，想不到这么快就把肉带来了\
真是太感谢你了\
你叫<$USERNAME>?\
这个名字将来一定会传遍整个大陆的\
这是我家祖传的一条传统项链\
送给你做个纪念吧!\ \
<接受/@new3_21>\
助人为快乐之本,<我不能要/@new3_22>

[@new3_21]
#if
#act
give 传统项链 1
#say
戴上它吧\
希望能对你有所帮助\
不要再留恋这个小村庄了\
勇敢的少年啊!快去创造奇迹!\ \
<再见/@exit>

[@new3_22]
#if
#act
give 疗伤药 1
#say
真的吗?\
你是我这么多年来见过的心肠最好的人了!\
既然你执意如此,我也不勉强你了\
我想你一定愿意再帮我一次.\
我有一个儿子在比奇城飞天武馆教人武功\
你帮我把这瓶疗伤药送给他吧!\
千万不要丢在路上啊!\
<询问/@ask>武馆的位置\
<确定/@exit>

[@ask]
飞天武馆在比奇城的右上方\
比奇城在比奇省的中部\
坐标300,300左右的地方\
祝你一路顺风!这样我再给你点路费,不要推辞哦\ \
<接受路费/@lufei>

[@lufei]
#if
#act
give 金币 20000
#say
收好,不要给人偷了哦\ \
<再见了/@exit>

[@news]
#if
checkitem 薄荷叶 1
#act
goto @quest7
#elseact 
goto @normal

[@normal]
#if
random 2
#say
我店里的许中医远近闻名,啥毛病都能治!\
他在比奇城里药店的父亲\
也是很有名的药剂师哦\
任务提示：如果得到疗伤药，请将药带到\
飞天武馆找黄飞龙。\ \
<返回/@main-1>
#elsesay
今天我要大宴宾客\
但是还缺少5块好鹿肉和5块好鸡肉\
怎么办啊?\
任务提示：如果得到疗伤药，请将药带到\
飞天武馆找黄飞龙。\ \
<返回/@main-1>

[@quest7]
#if
checklevel 15
#act
goto @new2_1
#elseact
goto @new2_2

[@new2_1]
#if
#act
take 薄荷叶 1
#say
这是那个经常和我吵架的屠夫送给我的薄荷叶吗?\
想不到他会这样....\
<$USERNAME>,你真不错啊!\
看在你的面子上这薄荷叶我就收下了!\ \
<确定/@exit>

[@new2_2]
#if
checkitem 肉 5 
checkitem 鸡肉 5
#act
goto @new3
#elsesay
这是那个莽撞的屠夫让你送来的吗?\
真想不到他的心挺细的\
真是谢谢你了!\
这薄荷叶还得你替我保管一会儿\
今晚的晚宴还缺一些材料啊\
你能帮我找来吗?\ \
<可以/@new2_21>,我很乐意帮助你\
<抱歉/@new2_22>,我很忙,帮不了你\

[@new2_22]
#if
#act
take 薄荷叶 1
#say
真是可惜\
再见了\ \
<再见/@exit>

[@new2_21]
真的吗?太好了\
请帮我找5块品质12以上的肉\
和5块品质5以上的鸡肉\
如果找到的话请尽快给我\ \
<接受/@new2_211>\
<太难了/@new2_212>,我完成不了

[@new2_212]
#if
#act
take 薄荷叶 1
#say
这样啊\
那我找别人帮忙吧!\ \
<确定/@exit>

[@new2_211]
在这个村子周围有很多鹿啊,鸡啊\
你能在这里得到足够的肉\ \
<确定/@exit>

[@makedrug]
请选择想炼什么药。\
所需材料应该带来了吧？\ \
 <返回/@Main>

[@LOOKLYSM]
这里可以炼的药只有两种。\ \
 <灰色毒粉/@HSYFGN0>的功能\
 <黄色毒粉/@HSYFGN1>的功能\
 <灰色毒粉/@HSYFYL0>的原料\
 <黄色毒粉/@HSYFYL1>的原料\ \
 <返回/@Main>

[@HSYFGN0]
道士可以在使施毒术时，用灰色毒粉，\
中毒会减掉体力。\ \
 <返回/@LOOKLYSM>
[@HSYFGN1]
道士可以在使施毒术时，用黄色毒粉，\
中毒会减掉防御能力。\ \
 <返回/@LOOKLYSM>
[@HSYFYL0]
灰色毒粉的原料有<食人树的果实/@helpitem0>1个，<毒蜘蛛牙齿>2个，\
<食人树叶>4个。\ \
 <返回/@LOOKLYSM>
[@HSYFYL1]
黄色毒粉的原料有<蛆卵/@helpitem1>1个，\
<蝎子的尾巴>1个，<食人树叶>4个。\ \
 <返回/@LOOKLYSM>

[@helpitem0]
割食人花可以得到食人树的果子，\
但是5个食人花中可以得到果实的只有1个。\ \
 <返回/@HSYFYL0>

[@helpitem1]
抓到洞穴里的洞蛆，就可以得到蛆卵。\
长得像圆圆的茧。\ \
 <返回/@HSYFYL1>

[goods]
灰色药粉(少量)	5	10
黄色药粉(少量)	5	10
灰色药粉(中量)	5	10
黄色药粉(中量)	5	10
灰色药粉(大量)	5	10
黄色药粉(大量)	5	10


;#####################################################

[@talkwith]
#IF
check  [1] 0
#ACT
goto  @talkwith_1


;#####################################################


[@talkwith_1]
;讲有关信件的事情
#IF
check [103] 1
check [104] 0
check [105] 0
#ACT
goto @talk_about_letter
break

;为了成为朋友，再回来
#IF
check  [103]  1
check  [104]  1
check  [106]  0
#ACT
goto @talk_1_1
break

;来给食人树的果实
#IF
check  [104]  1
check  [106]  1
check  [107]  0
#ACT
goto @talk_2_1
break

;为得到许中医的药而来
#IF
check  [104]  1
check  [106]  1
check  [107]  1
check  [108]  0
#ACT
goto 	@talk_3_1
break

;没有任何条件时的对话
#IF
check  [105] 1
#SAY
<$USERNAME> 对不起，我现在太忙。可不\
可以下次再来？对了你找到我师傅刘中医了吗？\
没有就赶快去比奇大城里问问赖家药店老板吧。\ \
 <确定/@exit>

#ACT
break

#IF
#SAY
对不起，我现在忙，以后再来找我吧。\ \
 <退出/@exit>


;-------------------------------------------------------
;为传达信件而来
;-------------------------------------------------------

[@talk_about_letter]
...就是说你带着我父亲的信件，\
因为什么事情给我写信的呢？\
快把信交给我。\ \
<传达信件/@give_letter>\
<信件丢了/@no_give_letter>\

[@give_letter]
#IF
CHECKITEM  药剂师的信 1
#ACT
TAKE  药剂师的信  1
SET   [104]   1
#SAY
谢谢，让你辛苦了。\
真太谢谢了，能告诉名字吗？\
我是许中医。\
你叫什么？\ \
<告诉名字/@say_my_name>\
#ELSESAY
现在没有信?\
你是说在路上丢了？找到之后马上给我。\ \
<退出/@exit>

[@no_give_letter]
什么？把信丢了？\
呵呵，好了，那也用不着这么客气，\
这样一来我就变得不好意思了，\
好了，没事了，你回去吧\ \
<退出/@exit>

[@say_my_name]
<$USERNAME> 嗨, 见到你很高兴。\
这也是缘分，我们友好相处吧。\
这是我的一点心意，收下吧。\
是叫做太阳神水的药水，很管用的。\ \
<收下/@ok_take_drug>

[@ok_take_drug]
#ACT
SET  [105]    1
GIVE 强效太阳水 1
#SAY
对了，下次一定再来。\
再见\ \
 <退出/@exit>


;-------------------------------------------------------
; 传达信之后来。给食人树的果实之前
;-------------------------------------------------------

[@talk_1_1]
#SAY
啊 <$USERNAME>, 欢迎光临 \
你过的还好吗？\
现在我需要食人树的果实\
如果你帮我弄到几个，我会给你好价钱的。\
请你一定帮我。\
那我就等你的好消息了。\ \
 <确定/@exit>
#ACT
SET  [106] 1

;-------------------------------------------------------
; 为交给食人树的果实而来
;-------------------------------------------------------

[@talk_2_1]
#IF
CHECKITEM  食人树的果实  1
#SAY
还是你好，没有忘记。\
拿来的话，就一个一个给吧。\
任务提示：成功几率为1/10，所以收集多点食人树\
的果实来慢慢交任务吧。\ \
<给一个/@give_sg_1>\
#ELSEACT
GOTO  @talk_1_1

[@give_sg_1]
#IF
CHECKITEM  食人树的果实 1
#ACT
TAKE 食人树的果实 1
GIVE 金币 600
GOTO @check_enough_1

;检查是否充分收到食人树的果实
[@check_enough_1]
#IF
RANDOM 10
#ACT
SET  [107] 1
#SAY
谢谢你 <$USERNAME> 现在食人树的果实已经够了。\
这都是你的功劳。\
现在我要开始炼药了，\
请你为我祈祷吧。\
再见\ \
<确定/@exit>
#ELSEACT
GOTO @give_sg_2

[@give_sg_2]
#IF
CHECKITEM 食人树的果实 1
#ACT
GOTO @talk_2_1
#ELSESAY
我还需要更多的食人树的果实，\
麻烦你再去帮我找点来。\ \
<确定/@exit>


;-------------------------------------------------------
;许中医正在炼第一个药
;-------------------------------------------------------

[@talk_3_1]
#IF
random 5
#act
goto 	@talk_3_2
#elsesay
<$USERNAME>, 欢迎光临，我的药还没有炼成。\
要是一旦炼成了，请你一定答应我一个请求，\
但愿快一点把药炼成。\
任务提示：继续任务的成功几率为1/5，多点几次对话\
即可完成任务并继续任务!\ \
<确定/@exit>

[@talk_3_2]
<$USERNAME>, 药是已经炼成了,\
但是还没有没有解决的部分，\
需要向师傅请教。\
有件事情想求你帮忙，你答应吗？\ \
<接受请求/@ok_h_order>\

[@ok_h_order]
谢谢你，你把这个药送给我师父刘中医，\
请教师傅这剂药缺少什么东西。\
我不能空着药房，所以就请你辛苦一次了。\
我师父以前和赖家药店老板很熟悉，但是好久\
都没有音信，很对不起，请你一定帮我这个忙。\
任务提示：去比奇大城赖家药店打听刘中医下落\ \
<接受请求/@ok_h_order_now>

[@ok_h_order_now]
#act
set  [108] 1
give 许中医的药1 1
close
